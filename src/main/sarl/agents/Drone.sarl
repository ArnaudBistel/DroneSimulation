package agents

import io.sarl.core.AgentKilled
import io.sarl.core.AgentSpawned
import io.sarl.core.ContextJoined
import io.sarl.core.ContextLeft
import io.sarl.core.Destroy
import io.sarl.core.Initialize
import io.sarl.core.Logging
import io.sarl.core.MemberJoined
import io.sarl.core.MemberLeft
import io.sarl.core.ParticipantJoined
import io.sarl.core.ParticipantLeft
import io.sarl.core.SpaceCreated
import io.sarl.core.SpaceDestroyed
import gui.DroneBody
import gui.WarehouseBody
import io.sarl.core.Lifecycle
import io.sarl.core.Schedules
import io.sarl.core.DefaultContextInteractions
import io.sarl.javafx.AppExit
import java.util.List
import map.MapPoint
import org.arakhne.afc.math.geometry.d2.i.Point2i
import gui.PathEnum
import map.MapPointType
import gui.Body
import utils.variables.SimulationParameters
import solutionSolver.SolutionSolver

/** 
 * @author arnaud
 * 
 */
agent Drone {
	uses Logging, Lifecycle, Schedules, DefaultContextInteractions

	var body : DroneBody
	var warehouseBody : WarehouseBody
	var path : List<MapPoint>
	var nextDestination : Point2i = null
	var arrivedToNextDestination : Boolean = true
	var backToWarehouse : Boolean = true

	
	on Initialize {
		synchronized (this) {
		
			// Event trigger before the agent begin to live or to be used.
			// You should put all the initialization statements in this block of code.
			// info("The agent Drone was started : " + this.getID)
			this.body = occurrence.parameters.get(0) as DroneBody
			this.warehouseBody = occurrence.parameters.get(1) as WarehouseBody
			this.body.actualBattery = SimulationParameters.DRONE_MAX_ENERGY
			emit(new AgentInitialized(AgentType.DRONE))
		}	
	}
	
	on DronePerception {
		synchronized (this) {
			if (this.nextDestination !== null) {
				// Checking if we arrive to next destination
				if (this.body.position.x == this.nextDestination.getX() &&
					this.body.position.y == this.nextDestination.getY()
				) {
					this.nextDestination = null
					this.arrivedToNextDestination = true
					if(this.path.size > 0){
						var action = new Action(ActionType.DEPOSIT, #[this.body.position.x as Integer, this.body.position.y as Integer])
						var actionEvent = new ActionEvent(action)
						emit(actionEvent)	
					} else {
						this.backToWarehouse = true
					}
					calcNextDestinationVelocity()
				} else {
					var bodies = occurrence.perceived as List<DroneBody>
					
					if (bodies.size() > 0) {
						// Si on est près de l'entrepot, on considère que les drones partent d'un étage différent
						if (Math.abs(this.warehouseBody.position.getX() - this.body.position.x) > 100
							&& Math.abs(this.warehouseBody.position.getY() - this.body.position.y) > 100) {
							// On va influer sur le drone le plus proche
							var closestDrone = bodies.get(0)
							var tempX = closestDrone.position.getX()
							var tempY = closestDrone.position.getY()
							for (var i=1 ; i<bodies.size() ; i++) {
								var tmpDrone = bodies.get(i)
								var tmpX = tmpDrone.position.getX()
								var tmpY = tmpDrone.position.getY()
								if (Math.abs(tmpX - this.body.position.x) + Math.abs(tmpY - this.body.position.y) <
									Math.abs(tempX - this.body.position.x) + Math.abs(tempY - this.body.position.y)) {
									closestDrone = bodies.get(0)
									tempX = tmpX 
									tempY = tmpY 
								}
							}
							if (Math.abs(this.body.velocityX) > Math.abs(this.body.velocityY)) {
								this.body.velocityX = this.body.velocityX / 2
								closestDrone.velocityY = closestDrone.velocityY / 2
							} else {
								this.body.velocityY = this.body.velocityY / 2
								closestDrone.velocityX = closestDrone.velocityX / 2
							}
						}
					}
					
					var x = this.body.posX + this.body.velocityX
					var y = this.body.posY + this.body.velocityY

					var distX = this.nextDestination.getX() - this.body.position.x
					var distY = this.nextDestination.getY() - this.body.position.y

					if (Math.abs(distX) <= 3 && Math.abs(distY) <= 3) {
						x = this.nextDestination.getX()
						y = this.nextDestination.getY()
					}

					computeBatteryLoss(x as int, y as int)
					this.body.setPosX(x)
					this.body.setPosY(y)

					calcNextDestinationVelocity()

					var action = new Action(ActionType.MOVE, #[x as Integer, y as Integer, this.ID])
					var actionEvent = new ActionEvent(action)
					emit(actionEvent)
				}
			}
			
		}
	}
	
	on DronePathAssignment {
		synchronized (this) {
			this.body.setImage(PathEnum.DRONE_LOADED.getPath)
			this.path = occurrence.path
			// Deleting the initial warehouse
			this.path.remove(0)
			this.body.actualCharge = 0
			for(point : this.path){
				this.body.actualCharge = this.body.actualCharge + point.packageWeight
			}
			
			this.backToWarehouse = false
			this.arrivedToNextDestination = false
			this.nextDestination = this.path.get(0).position
			calcNextDestinationVelocity()
			
		}
	}
	
	def computeBatteryLoss(x : int, y : int)  {
		var oldX = this.body.posX
		var oldY = this.body.posY
		var dist = Math.sqrt(Math.pow(x-oldX,2) + Math.pow(y-oldY, 2)) / 15
		this.body.actualBattery = this.body.actualBattery - SolutionSolver.droneEnergyCost(this.body.actualCharge, dist)
		
	}
	
	def calcNextDestinationVelocity() {
		if (!this.backToWarehouse) {
			if (this.path.size() == 1) {
				this.body.setImage(PathEnum.DRONE_EMPTY.getPath)
			}
			if (this.arrivedToNextDestination) {
				info("arrived")
				this.body.actualCharge = this.body.actualCharge - this.path.get(0).packageWeight
				this.nextDestination = this.path.remove(0).position
				this.arrivedToNextDestination = false
			}
			var distX = this.nextDestination.getX() - this.body.position.x
			var distY = this.nextDestination.getY() - this.body.position.y
			var totalDist = Math.sqrt(Math.pow(distX, 2) + Math.pow(distY, 2))
			var totalVelocity = 3.0;
			this.body.velocityX = totalVelocity * distX / totalDist;
			this.body.velocityY = totalVelocity * distY / totalDist;
			/*if (Math.abs(distX) >= Math.abs(distY)){
				var vX = 3
				if (distX < 0) vX = -3
				this.body.velocityX = vX
				this.body.velocityY = (vX * distY) / distX
			} else {
				var vY = 3
				if(distY < 0) vY = -3
				this.body.velocityY = vY
				this.body.velocityX = (vY * distX) / distY
			}*/
		} else { // back to warehouse
			var action = new Action(ActionType.BACK_TO_WAREHOUSE, #[this.body])
			var actionEvent = new ActionEvent(action)
			emit(actionEvent)
			this.body.actualBattery = SimulationParameters.DRONE_MAX_ENERGY
		}
	}

	on AppExit {
		//info("The agent Drone is stopping.")
		killMe
	}

	on Restart {
		killMe
	}

	on Destroy {
		//info("Drone destroy")
		// killMe
	}

	on AgentSpawned {
		// A new agent has been spawned in our context.
	}

	on AgentKilled {
		// A new agent has been killed in our context.
	}

	on ContextJoined {
		// Event trigger when an agent of the level joins a shared context.
		// Sent to a agent of the same level.
	}

	on ContextLeft {
		// Event trigger when an agent of the level jleaves a shared context.
		// Sent to a agent of the same level.
	}

	on MemberJoined {
		// Event trigger when an agent joins one of our shared super-holons.
		// Sent to all members of the considered super-holon.
	}

	on MemberLeft {
		// Event trigger when an agent leaves one of our shared super-holons.
		// Sent to all members of the considered super-holon.
	}

	on MemberLeft {
		// Event trigger when an agent leaves one of our shared super-holons.
		// Sent to all members of the considered super-holon.
	}

	on SpaceCreated {
		// Event trigger when a space is created into one of the contexts where the agent is.
	}

	on SpaceDestroyed {
		// Event trigger when a space is destroyed from one of the contexts where the agent is.
	}

	on ParticipantJoined {
		// Event trigger when a participant has joined a space.
	}

	on ParticipantLeft {
		// Event trigger when a participant has left a space.
	}
}
